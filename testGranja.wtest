import hector.*
import granjaYcosas.*
import cultivos.*
import wollok.game.*



describe "hector, granja, plantas y cosas" {

    const corn1 = new Corn( position = 0) 
    const corn2 = new Corn( position = 0) 
    const wheat1 = new Wheat( position = 0) 
    const aspersor1 = new Aspersor( position = 0 ) 

  method initialize(){
    game.height(10)
    game.width(10)
    game.addVisualCharacter(hector)
	  game.addVisual( mercado1 )
    //mercado 1 en (9,9)
  }

  test " sembrar " {
    hector.sembrar(corn1)

    assert.equals(#{corn1}, granja.cultivos())

    hector.position(  game.at(0,1) )
    hector.sembrar(corn2)

    assert.equals(#{corn1, corn2}, granja.cultivos())

    assert.throwsException({ hector.sembrar(wheat1) }) //porque ya hay una plantas all√≠ y no acepto 2 plantas en el mismo lugar

  }

  test " regar " {
    hector.sembrar(corn1)
    hector.regar()

    assert.equals("corn_adult.png", corn1.image())

    hector.position(  game.at(0,1) )
    hector.sembrar(wheat1)
    hector.regar()
    hector.regar()

    assert.equals(2, wheat1.madurez())

  }

  test " cosechar " {
    hector.sembrar(wheat1)
    hector.regar()

    assert.throwsException({ hector.cosechar() })

    hector.regar()
    hector.regar()
    hector.cosechar()

    assert.equals(#{wheat1}, hector.cosecha())
    assert.equals(#{}, granja.cultivos())

  }

  test " vender " {
    hector.sembrar(corn1)  //150
    hector.regar()
    hector.cosechar()
    hector.sembrar(corn2)  //150
    hector.regar()
    hector.cosechar()
    
    assert.equals(#{corn1, corn2}, hector.cosecha())
    assert.throwsException({hector.venderAqui()})

    hector.position( game.at(9,9) )
    hector.venderAqui()

    assert.equals(300, hector.ganancias())
    assert.equals(#{corn1, corn2}, mercado1.mercaderia())

  }

  test " dejar aspersor " {
    hector.sembrar(corn1)
    hector.position( game.at(1,0) )
    hector.dejarAspersor(aspersor1)
    aspersor1.regarPlantas()

    assert.equals(#{aspersor1}, granja.cosasJardineria())
    assert.equals("corn_adult.png", corn1.image())

  }
  
}
